apply plugin: 'com.android.application'
apply plugin: 'maven'

def googleMapsApiReleaseKey = hasProperty('RELEASE_MAPS_MAPCACHE_API_KEY') ? RELEASE_MAPS_MAPCACHE_API_KEY : ''
def googleMapsApiKeyDebug = hasProperty('DEBUG_MAPS_API_KEY') ? DEBUG_MAPS_API_KEY : ''


android {
    compileSdkVersion 28

    dexOptions {
        javaMaxHeapSize "4g"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        applicationId "mil.nga.mapcache"
        resValue "string", "applicationId", applicationId
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 23
        versionName '1.22'
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            resValue "string", "GOOGLE_MAPS_ANDROID_API_KEY", googleMapsApiReleaseKey
        }
        debug {
            applicationIdSuffix ".test"
            resValue "string", "applicationId", defaultConfig.applicationId + applicationIdSuffix
            minifyEnabled false
            resValue "string", "GOOGLE_MAPS_ANDROID_API_KEY", googleMapsApiKeyDebug
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }
}

task androidAppVersion {
    doLast {
        com.android.build.gradle.AppExtension appExt = project.extensions.android;
        println "versionCode: ${appExt.defaultConfig.applicationId}@${appExt.defaultConfig.versionCode}"
        println "versionName: ${appExt.defaultConfig.applicationId}@${appExt.defaultConfig.versionName}"
    }
}

dependencies {
    //    api 'com.android.support:appcompat-v7:27.1.1'
    //    api 'com.android.support:design:27.1.1'
    //    api 'com.android.support:cardview-v7:27.1.1'
    api 'com.android.support:appcompat-v7:28.0.0-beta01'
    api 'com.android.support:design:28.0.0-beta01'
    api 'com.android.support:cardview-v7:28.0.0-beta01'
    api 'android.arch.lifecycle:extensions:1.1.1'
    api 'mil.nga.geopackage.map:geopackage-android-map:3.0.2' // comment out to build locally
    //api project(':geopackage-map') // uncomment me to build locally
    api 'com.android.support:multidex:1.0.3'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    testImplementation 'com.android.support:multidex:1.0.3'
    configurations.all {
        exclude group: 'com.android.support', module: 'support-v13'
    }
}

configure extensions.android, {
    if (project.hasProperty('RELEASE_STORE_FILE') && new File((String) project.getProperty('RELEASE_STORE_FILE')).exists()) {
        signingConfigs {
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
        buildTypes {
            release {
                setSigningConfig(signingConfigs.release)
            }
        }
    } else {
        println 'no keystore configured'
    }
}
